/*
 * Copyright (C) 2005-2017 Qihoo 360 Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not
 * use this file except in compliance with the License. You may obtain a copy of
 * the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed To in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations under
 * the License.
 *
 */

plugins {
    id 'java'
    id 'groovy'
    id 'maven-publish'
}

repositories {
    google()
    mavenCentral()
}

group = 'com.qihoo360.replugin'  // 组名

String classPath = ".src.main.groovy.com.qihoo360.replugin.gradle.plugin.AppConstant".replace(".", java.io.File.separator)
String verPath = "${project.projectDir}" + classPath + ".groovy"
String verLine = new File(verPath).filterLine { it =~ /def static final VER =/ }
version = "${verLine.split("\"")[1]}" // 版本
//红色醒目打印显示版本号
java.lang.System.err.println "version=${version}"

sourceSets {
    main {
        groovy {
            srcDirs '../replugin-host-gradle/src/main/groovy/com/qihoo360/replugin/gradle/compat'
        }
    }
}

dependencies {
    implementation 'com.android.tools.build:gradle:7.4.2'
    implementation 'org.json:json:20160212'
    implementation 'org.codehaus.groovy:groovy:2.4.7'
    implementation 'com.squareup:javapoet:1.5.1'
    implementation 'com.android.tools.build:transform-api:1.5.0'
    implementation 'org.javassist:javassist:3.18.2-GA'
    implementation 'commons-io:commons-io:2.5'

    // gradle:7.4.2 也会依赖guava，codec
    implementation 'com.google.guava:guava:30.1-jre'
    implementation 'commons-codec:commons-codec:1.11'

    implementation gradleApi()
    implementation localGroovy()
}

project.ext.RP_ARTIFACT_ID = 'replugin-plugin-gradle'
apply from: '../rp-publish.gradle'